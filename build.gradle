plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.3.0"
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

configurations {
    implementation.extendsFrom shade
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }

    flatDir {
        dirs("libs")
    }
}

dependencies {
    // DB-ORM
    //implementation("de.tomalbrc:db-bundle:1.2.3")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //implementation "com.hypixel.hytale:Server:1.0.0" // TODO: once its on mavenCentral
    implementation(":HytaleServer")
}

shadowJar {
    configurations = [project.configurations.shade]
    mergeServiceFiles()
}

test {
    useJUnitPlatform()
}

processResources {
    var expandProps = [
            'name'       : project.name,
            'version'    : project.version,
            'group'      : project.group,
            'entry_point': project.entry_point
    ]

    filesMatching(['manifest.json']) {
        expand expandProps
    }
}

def serverRunDir = file("$projectDir/run")
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=\"/Users/tomalbrecht/Library/Application Support/Hytale/install/release/package/game/latest/Assets.zip\" --mods=${file("src/main/").absolutePath} --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}
